CompilationUnit ::= Statement*;

// Expressions:

abstract Expr;

QExpr: Expr ::= <Quantifier:de.unikl.bcverifier.isl.parser.Quantifier> BoundVar:VarDef* Expr;

VarAccess: Expr ::= Name:Ident;

MemberAccess: Expr ::= Left:Expr Right:Ident;

FuncCall : Expr ::= FuncName:Ident Argument:Expr*;

BinaryOperation : Expr ::= <Operator:de.unikl.bcverifier.isl.parser.BinaryOperator> Left:Expr Right:Expr;
UnaryOperation : Expr ::= <Operator:de.unikl.bcverifier.isl.parser.UnaryOperator> Expr;
InstanceofOperation : Expr ::=
Left:Expr Right:TypeDef;

IfThenElse : Expr ::= Cond:Expr ThenExpr:Expr ElseExpr:Expr;

ErrorExpr : Expr ::= <Message>;

// constant expressions:
BoolConst : Expr ::= <Val:boolean>;
IntConst : Expr ::= <Val>;
VersionConst : Expr ::= <Val:de.unikl.bcverifier.isl.ast.Version>;
NullConst : Expr ::=;


// Variable definitions:
abstract Def;

UnknownDef : Def ::= ;
VarDef : Def ::= TypeDef Name:Ident;

// Statements:
abstract Statement : Def;

Assign : Statement ::= Name:Ident Expr;
Invariant : Statement ::= Expr;
LocalInvariant: Statement ::= Expr;
PlaceDef: Statement ::= PlaceModifier PlaceOption:Ident* Name:Ident ProgramPoint:ProgramPointExpr [Condition:Expr] [StallCondition:StallCondition] Assignments:Assign* ;
ProgramPoint: Statement ::= Name:Ident ProgramPointExpr;
GlobVarDef : Statement ::= Var:VarDef InitialValue:Expr;

StallCondition ::= Condition:Expr [Measure:Expr];

abstract ProgramPointExpr;

LineNrProgramPoint: ProgramPointExpr ::= TypeDef <ProgramLineNr:int>;
CallProgramPoint: LineNrProgramPoint ::= FunctionName:Ident TypeDef <ProgramLineNr:int>;


abstract PlaceModifier;
PlaceModifierLocal: PlaceModifier ::= ;
PlaceModifierPredefined: PlaceModifier ::= ;

// Types

abstract TypeDef;

NamedTypeDef : TypeDef ::= <Version:Version> Name:Ident*;




// other stuff:

Ident ::= <Name>;